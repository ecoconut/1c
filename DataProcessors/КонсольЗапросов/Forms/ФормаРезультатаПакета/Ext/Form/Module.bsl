

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИмяТаблицы", ИмяТаблицы);
	Параметры.Свойство("АдресВременныхТаблиц", АдресВременныхТаблиц);
	
	Элементы.Комментарий.Заголовок = ИмяТаблицы;
	
	ПоказатьРезультат();
	
КонецПроцедуры 

&НаСервере
Процедура ПоказатьРезультат()
	
	Попытка
		сткВременныеТаблицы = ПолучитьИзВременногоХранилища(АдресВременныхТаблиц);
		
		тзВременная = сткВременныеТаблицы[ИмяТаблицы];
		                                                		
		ПодготовитьКоллекцию(тзВременная.Колонки);
		ЗначениеВРеквизитФормы(тзВременная, "РезультатПакета");
	Исключение
		Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), СтатусСообщения.Важное);
	КонецПопытки;
	
КонецПроцедуры 

&НаСервере
Процедура ПодготовитьКоллекцию(КоллекцияКолонок)
	
	УдаляемыеРеквизиты = Новый Массив;
	УдаляемыеЭлементы  = Новый Массив;
	Для Каждого Элемент Из Элементы.РезультатПакета.ПодчиненныеЭлементы Цикл 
		УдаляемыеРеквизиты.Добавить(Элемент.ПутьКДанным);
		УдаляемыеЭлементы.Добавить(Элементы[Элемент.Имя]);
	КонецЦикла;
	
	Для Каждого Элемент Из УдаляемыеЭлементы Цикл 
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого Колонка Из КоллекцияКолонок Цикл 
		Если Колонка.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) ИЛИ Колонка.ТипЗначения.СодержитТип(Тип("МоментВремени")) Тогда 
			Продолжить;
		КонецЕсли;
		
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "РезультатПакета", Колонка.Заголовок));
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Для Каждого Колонка Из КоллекцияКолонок Цикл 
		Если Колонка.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) ИЛИ Колонка.ТипЗначения.СодержитТип(Тип("МоментВремени")) Тогда 
			Продолжить;
		КонецЕсли;
		
		НовыйЭлемент = Элементы.Добавить("РезультатПакета" + Колонка.Имя, Тип("ПолеФормы"), Элементы.РезультатПакета);
		НовыйЭлемент.ПутьКДанным    = "РезультатПакета." + Колонка.Имя;
		НовыйЭлемент.Вид            = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ТолькоПросмотр = Истина;
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура РезультатПакетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.РезультатПакета.ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено Тогда 
		ОткрытьЗначение(ТекущиеДанные[СтрЗаменить(Поле.Имя, "РезультатПакета", "")]);
	КонецЕсли;
	
КонецПроцедуры 

