
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("АдресВременныхТаблиц", АдресВременныхТаблиц);
	ПоказатьРезультат();
КонецПроцедуры

&НаСервере
Процедура ПоказатьРезультат()
	
	Попытка
		сткВременныеТаблицы = ПолучитьИзВременногоХранилища(АдресВременныхТаблиц);
		
		обРезультат = Новый ТаблицаЗначений;
		обРезультат.Колонки.Добавить("ИмяТаблицы", Новый ОписаниеТипов("Строка"));
		обРезультат.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		
		Для Каждого Строка из сткВременныеТаблицы Цикл 
			НовСтрока = обРезультат.Добавить();
			НовСтрока.ИмяТаблицы = Строка.Ключ;
			НовСтрока.Количество = Строка.Значение.Количество();
		КонецЦикла;   		
		
		ЗначениеВРеквизитФормы(обРезультат, "Результат");
	Исключение
		Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), СтатусСообщения.Важное);
	КонецПопытки;
	
КонецПроцедуры 

&НаКлиенте
Процедура РезультатПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РезультатВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда Возврат; КонецЕсли;
	
	стрПарамтр = Новый Структура;
	стрПарамтр.Вставить("ИмяТаблицы", ТекущиеДанные.ИмяТаблицы);
	стрПарамтр.Вставить("АдресВременныхТаблиц", АдресВременныхТаблиц);
	
	ОткрытьФорму("Обработка.КонсольЗапросов.Форма.ФормаРезультатаПакета", стрПарамтр); 	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	СохранитьИзмененияНаСервере();
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура СохранитьИзмененияНаСервере()
	
	сткВременныеТаблицы = ПолучитьИзВременногоХранилища(АдресВременныхТаблиц);
	
	сткНовый = Новый Структура;
	
	Для Каждого Строка из Результат Цикл
		Если сткВременныеТаблицы.Свойство(Строка.ИмяТаблицы) Тогда
			сткНовый.Вставить(Строка.ИмяТаблицы, сткВременныеТаблицы[Строка.ИмяТаблицы]);
		КонецЕсли;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(сткНовый, АдресВременныхТаблиц);
	
КонецПроцедуры
